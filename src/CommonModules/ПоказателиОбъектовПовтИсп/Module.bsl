#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьСсылкуОбъектаМетаданных(ПолноеИмяМетаданных) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПолноеИмя", ПолноеИмяМетаданных);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОбъектыМетаданныхПО.Ссылка
		|ИЗ
		|	Справочник.ОбъектыМетаданныхПО КАК ОбъектыМетаданныхПО
		|ГДЕ
		|	ОбъектыМетаданныхПО.ПолноеИмя = &ПолноеИмя";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если (Выборка.Следующий()) Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ОбъектМетаданыхЯвляетсяТриггером(ПолноеИмяМетаданных) Экспорт
	
	СсылкаНаОбъектМетаданных = ПолучитьСсылкуОбъектаМетаданных(ПолноеИмяМетаданных);
	
	Если (СсылкаНаОбъектМетаданных = Неопределено) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// #РеквизитыОбъектов
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъектМетаданных);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбъектыМетаданныхПО.Ссылка
		|ИЗ
		|	Справочник.ОбъектыМетаданныхПО КАК ОбъектыМетаданныхПО
		|ГДЕ
		|	ОбъектыМетаданныхПО.Ссылка = &Ссылка
		|	И ОбъектыМетаданныхПО.ЯвляетсяТриггером";
		
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ОбъектМетаданныхМожетИметьПоказатели(ПолноеИмяМетаданных) Экспорт
	
	СсылкаНаОбъектМетаданных = ПолучитьСсылкуОбъектаМетаданных(ПолноеИмяМетаданных);
	
	Если (СсылкаНаОбъектМетаданных = Неопределено) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// #РеквизитыОбъектов
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъектМетаданных);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбъектыМетаданныхПО.Ссылка
		|ИЗ
		|	Справочник.ОбъектыМетаданныхПО КАК ОбъектыМетаданныхПО
		|ГДЕ
		|	ОбъектыМетаданныхПО.Ссылка = &Ссылка
		|	И ОбъектыМетаданныхПО.МожетИметьПоказатели";
		
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ЗависимыеПоказателиОбъектаМетаданных(ПолноеИмяМетаданных) Экспорт
	
	Результат = Новый Массив;
	
	ОбъектМетаданныхСсылка = ПолучитьСсылкуОбъектаМетаданных(ПолноеИмяМетаданных);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданныхСсылка);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПоказателиОбъектовТриггеры.Ссылка КАК Показатель,
		|	ЕСТЬNULL(ПоказателиОбъектовТриггеры.Ключ.Наименование, """") КАК Ключ,
		|	ПоказателиОбъектовТриггеры.ОтложенныйРасчет КАК ОтложенныйРасчет
		|ИЗ
		|	ПланВидовХарактеристик.ПоказателиОбъектов.Триггеры КАК ПоказателиОбъектовТриггеры
		|ГДЕ
		|	ПоказателиОбъектовТриггеры.ОбъектМетаданных = &ОбъектМетаданных
		|	И ПоказателиОбъектовТриггеры.Ссылка.ЗапускатьРасчетПоТриггерам";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПоказательСтруктура = Новый Структура("Показатель, Ключ, ОтложенныйРасчет");
		ЗаполнитьЗначенияСвойств(ПоказательСтруктура, Выборка);
		
		Результат.Добавить(ПоказательСтруктура);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти